// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {ERC20Permit} from "@openzeppelin/contracts/token/ERC20/extensions/ERC20Permit.sol";
import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";

/// @title TestToken — demo ERC-20 (tUSD 6 dp, tETH 18 dp) with a rate-limited public faucet.
contract TestToken is ERC20, ERC20Permit, Ownable {
    uint8 private immutable _decimals;
    uint256 public immutable faucetAmount;
    uint256 public immutable faucetCooldown;
    mapping(address => uint256) public lastFaucetAt;

    error FaucetCooldown(uint256 availableAt);

    constructor(string memory name_, string memory symbol_, uint8 decimals_, uint256 faucetAmount_, uint256 faucetCooldown_)
        ERC20(name_, symbol_) ERC20Permit(name_) Ownable(msg.sender)
    {
        _decimals = decimals_;
        faucetAmount = faucetAmount_;
        faucetCooldown = faucetCooldown_;
    }

    function decimals() public view override returns (uint8) {
        return _decimals;
    }

    function faucet() external {
        uint256 last = lastFaucetAt[msg.sender];
        if (last != 0 && block.timestamp < last + faucetCooldown) revert FaucetCooldown(last + faucetCooldown);
        lastFaucetAt[msg.sender] = block.timestamp;
        _mint(msg.sender, faucetAmount);
    }

    function mint(address to, uint256 amount) external onlyOwner {
        _mint(to, amount);
    }
}
